import groovy.xml.XmlParser

plugins {
    id 'java'
}

tasks.register("dist", Zip) {

    dependsOn("build")

    from("build/libs") {
        include("*.jar")
    }

    String pluginVersion
    if (project.hasProperty('pluginVersion')) {
        pluginVersion = project.getProperty('pluginVersion')
    } else {
        def pluginNode = new XmlParser().parse(new File('src/main/zip/plugin.xml'))
        pluginVersion = "${pluginNode.header.identifier.@version[0]}.dev"
    }

    from("src/main/zip") {
        filter {
            it.replace("@RELEASE_VERSION@", "$pluginVersion")
        }
    }

    destinationDirectory.set(file("build/distributions"))
    archiveFileName = "${rootProject.name}-${pluginVersion}.zip"
}